// Available to customize the build
plugins {
	id "de.undercouch.download" version "3.3.0"
}
apply plugin: 'java'

task wrapper(type: Wrapper) {
  jarFile = rootProject.file('.gradle-wrapper/gradle-wrapper.jar')
}

// download tasks
import de.undercouch.gradle.tasks.download.Download
import de.undercouch.gradle.tasks.download.Verify

task downloadEmf(type: Download) {
    src 'http://download.eclipse.org/modeling/emf/emf/downloads/drops/2.13.0/R201706090928/emf-xsd-Update-2.13.0.zip'
    dest "${bnd_cnf}/cache/p2drops/emf-xsd-Update-2.13.0.zip"
    onlyIfModified true
}

// might not be needed if bnd checks hashes
task verifyEmf(type: Verify, dependsOn: downloadEmf) {
    src new File("${bnd_cnf}/cache/p2drops", 'emf-xsd-Update-2.13.0.zip')
    algorithm 'MD5'
    // checksum cannot be a closure
    checksum '87282b4dbfb793908262c7d4ee900517'
}

task unzipEmf(dependsOn: verifyEmf, type: Copy) {
    from zipTree(downloadEmf.dest)
    into "${bnd_cnf}/emf"
	doLast {
	    bndWorkspace.repositories.findResult{ it.name == 'EMF' ? it : null }.refresh()
	    println 'Refreshed EMF'
	}
}

build.dependsOn unzipEmf

// Dust if you must
//clean.doFirst {
//    delete "${bnd_cnf}/emf"
//    delete "${bnd_cnf}/cache/p2drops/emf-xsd-Update-2.13.0.zip"
//}
